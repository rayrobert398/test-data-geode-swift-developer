buildscript {
  ext {
    snapshotBuild = version.endsWith('SNAPSHOT')
    releaseBuild = version.endsWith('RELEASE')
    milestoneBuild = !(releaseBuild || snapshotBuild)
  }
  repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://repo.spring.io/plugins-release' }
    maven {
      url 'https://repo.spring.io/plugins-snapshot'
      if (project.hasProperty('artifactoryUsername')) {
        credentials {
          username "$artifactoryUsername"
          password "$artifactoryPassword"
        }
      }
    }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
  }
}

apply plugin: 'io.spring.convention.root'

allprojects {

  group = 'org.springframework.data'

  repositories {

    mavenCentral()

    if (version.contains('-')) {
      maven { url "https://repo.spring.io/milestone" }
    }
    if (version.endsWith('-SNAPSHOT')) {
      maven { url "https://repo.spring.io/snapshot" }
    }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "minutes"
  }
}

subprojects {
  plugins.withType(JavaPlugin) {
    sourceCompatibility = JavaVersion.VERSION_17
  }
}

description = 'Spring Test Framework for Apache Geode'

//ext['spring.version'] = "$springVersion"
ext['spring-framework.version'] = "$springVersion"
ext['spring-data-bom.version'] = "$springDataBomVersion"

nohttp {
  source.exclude "**/target/**", "buildSrc/build/**"
}
